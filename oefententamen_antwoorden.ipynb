{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "vscode": {
     "languageId": "dotnet-interactive.fsharp"
    }
   },
   "source": [
    "# Oefententamen functioneel programmeren 2021-2022\n",
    "\n",
    "Dit oefententamen heeft dezelfde vorm en puntenverdeling als het echte tentamen.\n",
    "\n",
    "Het tentamen bestaat uit twee delen:\n",
    "- 3 Begrips- en programmeervragen (60 punten)\n",
    "- 1 RxAvansvraag (30 punten)\n",
    "\n",
    "Bij 45 van de 90 punten heb je een 5,5. \n",
    "\n",
    "Alle vragen vind je in dit notebook. Lever dit notebook weer in via Remindo voordat de tijd voorbij is!\n",
    "\n",
    "Veel succes!"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Vraag 1\n",
    "20 punten (2 punten per onderdeel)\n",
    "\n",
    "Hieronder vind je een stukje code. Geef voor ieder van deze concepten of programmeertechnieken aan of ze toegepast zijn in het stukje code. Als ze worden toegepast geef je ook aan op welke plek dat is.\n",
    "\n",
    "Geef antwoord door het commentaar bovenaan de code aan te passen. Geef aan waar het in de code wordt toegepast door commentaar in de code te plaatsen.\n",
    "\n",
    "1. Wordt er een closure gebruikt? Zo ja, waar?\n",
    "2. Wordt functiecompositie gebruikt? Zo ja, waar?\n",
    "3. Wordt partial function application gebruikt? Zo ja, waar?\n",
    "4. Wordt er recursie gebruikt? Zo ja, waar?\n",
    "5. Wordt pattern matching gebruikt? Zo ja, waar?\n",
    "6. Wordt er een pure functie gebruikt? Zo ja, waar?\n",
    "7. Wordt er een onpure (niet puur) functie gebruikt? Zo ja, waar?\n",
    "8. Wordt er een tuple gebruikt? Zo ja, waar?\n",
    "9. Wordt er een discriminated union (enum) gebruikt? Zo ja, waar?\n",
    "10. Wordt er een lambda functie gebruikt? Zo ja, waar?"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "vscode": {
     "languageId": "dotnet-interactive.fsharp"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Player One won with (5, 2)!\r\n"
     ]
    }
   ],
   "source": [
    "// 1. Closure: wel\n",
    "// 2. Functiecompositie: niet\n",
    "// 3. Partial function application: wel\n",
    "// 4. Recursie: wel\n",
    "// 5. Pattern matching: wel\n",
    "// 6. Pure functie: wel\n",
    "// 7. Onpure functie: wel\n",
    "// 8. Tuple: wel\n",
    "// 9. Discriminated union (enum): wel\n",
    "// 10. Lambda: wel\n",
    "\n",
    "type Hand = // enum\n",
    "    | Rock\n",
    "    | Paper\n",
    "    | Scissors\n",
    "\n",
    "type Winner = // enum\n",
    "    | One\n",
    "    | Two\n",
    "    | Neither\n",
    "\n",
    "let winner a b = // pure functie\n",
    "    match (a, b) with // pattern matching, tuple\n",
    "        | (Rock, Paper) -> Two\n",
    "        | (Paper, Scissors) -> Two\n",
    "        | (Scissors, Rock) -> Two\n",
    "        | (Paper, Rock) -> One\n",
    "        | (Scissors, Paper) -> One\n",
    "        | (Rock, Scissors) -> One\n",
    "        | _ -> Neither\n",
    "\n",
    "let rec makeGame strategyOne strategyTwo score = // recursie, pure functie\n",
    "    match (winner (strategyOne ()) (strategyTwo ()), score) with // pattern matching, tuple\n",
    "        | (_, (5, _)) -> printfn \"Player One won with %O!\" score\n",
    "        | (_, (_, 5)) -> printfn \"Player Two won with %O!\" score\n",
    "        | (One, (scoreOne, scoreTwo)) -> makeGame strategyOne strategyTwo (scoreOne + 1, scoreTwo)\n",
    "        | (Two, (scoreOne, scoreTwo)) -> makeGame strategyOne strategyTwo (scoreOne, scoreTwo + 1)\n",
    "        | (Neither, _) -> makeGame strategyOne strategyTwo score\n",
    "\n",
    "let makeConstantAI hand () = hand // pure functie\n",
    "\n",
    "let makeRandomAI () = // onpure functie\n",
    "    let rnd = (new System.Random())\n",
    "    fun _ -> // onpure functie, closure (rnd), lambda\n",
    "        match rnd.Next(0, 3) with\n",
    "            | 0 -> Rock\n",
    "            | 1 -> Paper\n",
    "            | 2 -> Scissors\n",
    "\n",
    "let play = makeGame (makeConstantAI Rock) (makeRandomAI ()) // partial application (makeGame en makeConstantAI)\n",
    "\n",
    "play (0,0)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Vraag 2.a\n",
    "10 punten\n",
    "\n",
    "Schrijf een pure recursieve functie die een lijst van integer opties afgaat en alleen de integers eruit haalt. Gebruik hierbij pattern matching.\n",
    "\n",
    "Voorbeeld (dubbelklik voor copy):\n",
    "```F#\n",
    "removeNone [Some 1; None; Some 3; None; Some 5; None; Some 7; None] // geeft [1; 3; 5; 7]\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "vscode": {
     "languageId": "dotnet-interactive.fsharp"
    }
   },
   "outputs": [
    {
     "data": {
      "text/html": [
       "<table><thead><tr><th><i>index</i></th><th>value</th></tr></thead><tbody><tr><td>0</td><td><div class=\"dni-plaintext\">1</div></td></tr><tr><td>1</td><td><div class=\"dni-plaintext\">3</div></td></tr><tr><td>2</td><td><div class=\"dni-plaintext\">5</div></td></tr><tr><td>3</td><td><div class=\"dni-plaintext\">7</div></td></tr></tbody></table>"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "let rec removeNone ls = \n",
    "    match ls with\n",
    "        | [] -> []\n",
    "        | None :: tail -> removeNone tail\n",
    "        | Some i :: tail -> i :: removeNone tail\n",
    "\n",
    "removeNone [Some 1; None; Some 3; None; Some 5; None; Some 7; None]"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Vraag 2.b\n",
    "10 punten\n",
    "\n",
    "Schrijf een pure **tail** recursieve functie die alle strings in een lijst aan elkaar plakt (concateneren). In plaats van alleen het eindresultaat terug te geven, moet de functie ieder tussenresultaat in een lijst opslaan en teruggeven. Gebruik hierbij pattern matching.\n",
    "\n",
    "Voorbeeld:\n",
    "```F#\n",
    "concatAll [\"hello\"; \"world\"; \"I\"; \"am\"; \"F#\"] [] // geeft [\"hello world I am F#\"; \"hello world I am\"; \"hello world I\"; \"hello world\"; \"hello\"]\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "vscode": {
     "languageId": "dotnet-interactive.fsharp"
    }
   },
   "outputs": [
    {
     "data": {
      "text/html": [
       "<table><thead><tr><th><i>index</i></th><th>value</th></tr></thead><tbody><tr><td>0</td><td>hello world I am F#</td></tr><tr><td>1</td><td>hello world I am</td></tr><tr><td>2</td><td>hello world I</td></tr><tr><td>3</td><td>hello world</td></tr><tr><td>4</td><td>hello</td></tr></tbody></table>"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "let rec concatAll ls result =\n",
    "    match (ls, result) with\n",
    "        | ([], _) -> result\n",
    "        | (head :: tail, []) -> concatAll tail [head]\n",
    "        | (head :: tail, prev :: _) -> concatAll tail ((prev + \" \" + head) :: result)\n",
    "\n",
    "concatAll [\"hello\"; \"world\"; \"I\"; \"am\"; \"F#\"] []"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Vraag 3.a\n",
    "10 punten\n",
    "\n",
    "In deze vraag schrijf je een functie die de eerste 3 characters and laatste 2 characters van een string afhaalt. Dit moet wel op een generieke manier, zodat we later andere implementaties zouden kunnen maken.\n",
    "\n",
    "1. Schrijf de functie `removeLast: int -> string -> string` die een getal `n` krijgt en zo veel characters aan het eind van de meegegeven string eraf haalt.\n",
    "2. Schrijf de functie `removeFirst: int -> string -> string` die een getal `n` krijgt en zo veel characters aan het eind van de meegegeven string eraf haalt.\n",
    "3. Maak de functie `removeLast2` op basis van de functie `removeLast`. Gebruik partial application.\n",
    "4. Maak de functie `removeFirst3` op basis van de functie `removeFirst`. Gebruik partial application.\n",
    "5. Maak de functie `removeLast2First3` op basis van de functies `removeLast2` en `removeFirst3`. Gebruik compositie.\n",
    "\n",
    "Voorbeeld:\n",
    "```F#\n",
    "removeLast2First3 \"532first message12\" // first message\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "vscode": {
     "languageId": "dotnet-interactive.fsharp"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "first message"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "let removeLast n (s: string) = s.Remove(s.Length - n)\n",
    "let removeFirst n (s: string) = s.Remove(0,n)\n",
    "\n",
    "let removeLast2 = removeLast 2\n",
    "let removeFirst3 = removeFirst 3\n",
    "\n",
    "let removeLast2First3 = removeLast2 << removeFirst3\n",
    "\n",
    "removeLast2First3 \"532first message12\" // first message"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Vraag 3.b\n",
    "10 punten\n",
    "\n",
    "We willen log messages opslaan en ophalen wanneer nodig. De log messages hebben onnodige characters aan het begin en aan het eind van de string. Die moet je eraf halen met `removeLast2First3` van opgave 3.a.\n",
    "\n",
    "Schrijf de functie `makeLogger` die twee functies in een tuple teruggeeft. `log` logt een bericht (bewaart het bericht in een lijst van strings), `get` geeft alle berichten tot nu toe in een lijst terug. Je mag hierbij mutable variabelen gebruiken.\n",
    "\n",
    "Voorbeeld:\n",
    "```F#\n",
    "let (log, get) = makeLogger()\n",
    "\n",
    "log \"532first message12\"\n",
    "log \"321second message56\"\n",
    "log \"790third message23\"\n",
    "\n",
    "get ()\n",
    "// third message\n",
    "// second message\n",
    "// first message\n",
    "\n",
    "// uitkomst zonder 3.a:\n",
    "// raw: 532first message12\n",
    "// raw: 321second message56\n",
    "// raw: 790third message23\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "vscode": {
     "languageId": "dotnet-interactive.fsharp"
    }
   },
   "outputs": [
    {
     "ename": "Error",
     "evalue": "input.fsx (3,24)-(3,41) typecheck error The value or constructor 'removeLast2First3' is not defined.",
     "output_type": "error",
     "traceback": [
      "input.fsx (3,24)-(3,41) typecheck error The value or constructor 'removeLast2First3' is not defined."
     ]
    }
   ],
   "source": [
    "let makeLogger () =\n",
    "    let mutable mem = []\n",
    "    let log s = mem <- removeLast2First3 s :: mem\n",
    "    let get () = mem\n",
    "    (log, get)\n",
    "\n",
    "let (log, get) = makeLogger ()\n",
    "\n",
    "log \"532first message12\"\n",
    "log \"321second message56\"\n",
    "log \"790third message23\"\n",
    "\n",
    "get ()\n",
    "// third message\n",
    "// second message\n",
    "// first message"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Vraag 4 (RxAvans)\n",
    "30 punten\n",
    "\n",
    "We willen een aantal waarden die door de reactieve keten komen samen kunnen nemen en in een lijst doorsturen: een soort buffer. De buffer operator krijgt een getal `n` mee. Wanneer er een item binnenkomt slaat de buffer deze op, totdat er `n` items binnen zijn, dan stuur hij alle waarden tegelijk in een lijst door naar de volgende in de keten.\n",
    "\n",
    "Voorbeeld (dubbelklik voor copy): \n",
    "```F#\n",
    "let producer observer = for i in 1..9 do observer i\n",
    "\n",
    "let buffered = buffer 3 producer\n",
    "\n",
    "buffered (printfn \"received: %A\")\n",
    "// received: [3; 2; 1]\n",
    "// received: [6; 5; 4]\n",
    "// received: [9; 8; 7]\n",
    "```\n",
    "\n",
    "Daarna willen we natuurlijk ook kunnen debufferen, dat wil zeggen, een lijst binnenkrijgen en de items in de lijst één voor één verder door de keten sturen.\n",
    "\n",
    "Voorbeeld:\n",
    "```F#\n",
    "let producer observer = observer [3; 2; 1]\n",
    "\n",
    "let debuffered = debuffer producer\n",
    "\n",
    "debuffered (printfn \"received: %A\")\n",
    "// received: 3\n",
    "// received: 2\n",
    "// received: 1\n",
    "```\n",
    "\n",
    "Schrijf beide operatoren (15 punten voor `buffer` en 15 punten voor `debuffer`). Hieronder vind je de RxAvansvoorbeeldcode uit het huiswerk, gebruik het ter inspiratie. Eronder vind je een toepassing van de operatoren dat je kan gebruiken om te testen, maar je kan ook je eigen testen schrijven."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "vscode": {
     "languageId": "dotnet-interactive.fsharp"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "received: [3; 2; 1]\r\n",
      "received: [6; 5; 4]\r\n",
      "received: [9; 8; 7]\r\n"
     ]
    }
   ],
   "source": [
    "let buffer amount previous =\n",
    "    let mutable mem = []\n",
    "    fun next ->\n",
    "        previous (fun data ->\n",
    "            mem <- data :: mem\n",
    "            if mem.Length >= amount then \n",
    "                next mem\n",
    "                mem <- []\n",
    "        )\n",
    "\n",
    "\n",
    "let producer observer = for i in 1..9 do observer i\n",
    "\n",
    "let buffered = buffer 3 producer\n",
    "\n",
    "buffered (printfn \"received: %A\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "vscode": {
     "languageId": "dotnet-interactive.fsharp"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "received: 3\r\n",
      "received: 2\r\n",
      "received: 1\r\n"
     ]
    }
   ],
   "source": [
    "let debuffer previous = \n",
    "    fun next -> (\n",
    "        previous (fun data -> \n",
    "            for i in data do next i\n",
    "        )\n",
    "    )\n",
    "\n",
    "\n",
    "let producer observer = observer [3; 2; 1]\n",
    "\n",
    "let debuffered = debuffer producer\n",
    "\n",
    "debuffered (printfn \"received: %A\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "vscode": {
     "languageId": "dotnet-interactive.fsharp"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "before: 1\r\n",
      "before: 2\r\n",
      "middle: [2; 1]\r\n",
      "after: 2\r\n",
      "received: 2\r\n",
      "after: 1\r\n",
      "received: 1\r\n",
      "before: 3\r\n",
      "before: 4\r\n",
      "middle: [4; 3]\r\n",
      "after: 4\r\n",
      "received: 4\r\n",
      "after: 3\r\n",
      "received: 3\r\n",
      "before: 5\r\n",
      "before: 6\r\n",
      "middle: [6; 5]\r\n",
      "after: 6\r\n",
      "received: 6\r\n",
      "after: 5\r\n",
      "received: 5\r\n"
     ]
    }
   ],
   "source": [
    "let log previous =\n",
    "    fun next ->\n",
    "        previous (fun data -> \n",
    "            printfn \"logging: %i\" data\n",
    "            next data\n",
    "        )\n",
    "\n",
    "let tap func previous = \n",
    "    fun next ->\n",
    "        previous (fun data ->\n",
    "            func data\n",
    "            next data\n",
    "        )\n",
    "\n",
    "let filter predicate previous =\n",
    "    fun next -> \n",
    "        previous (fun data -> \n",
    "            if predicate data then next data\n",
    "        )\n",
    "\n",
    "let map mapper previous =\n",
    "    fun next ->\n",
    "        previous (fun data ->\n",
    "            next (mapper data)\n",
    "        )\n",
    "\n",
    "let take amount previous =\n",
    "    let mutable count = 0\n",
    "    fun next ->\n",
    "        previous (fun data ->\n",
    "            if count < amount then next data\n",
    "            count <- count + 1\n",
    "        )\n",
    "\n",
    "let skip amount previous =\n",
    "    let mutable count = 0\n",
    "    fun next ->\n",
    "        previous (fun data -> \n",
    "            if (count >= amount) then next data\n",
    "            count <- count + 1\n",
    "        )\n",
    "\n",
    "\n",
    "let producer observer = for i in 1..6 do observer i\n",
    "\n",
    "let loggedBefore = tap (printfn \"before: %i\") producer\n",
    "let buffered = buffer 2 loggedBefore\n",
    "let loggedMiddle = tap (printfn \"middle: %A\") buffered\n",
    "let debuffered = debuffer loggedMiddle\n",
    "let loggedAfter = tap (printfn \"after: %i\") debuffered\n",
    "\n",
    "loggedAfter (printfn \"received: %i\")\n",
    "// before: 1\n",
    "// before: 2\n",
    "// middle: [2; 1]\n",
    "// after: 2\n",
    "// received: 2\n",
    "// after: 1\n",
    "// received: 1\n",
    "// before: 3\n",
    "// before: 4\n",
    "// middle: [4; 3]\n",
    "// after: 4\n",
    "// received: 4\n",
    "// after: 3\n",
    "// received: 3\n",
    "// before: 5\n",
    "// before: 6\n",
    "// middle: [6; 5]\n",
    "// after: 6\n",
    "// received: 6\n",
    "// after: 5\n",
    "// received: 5"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": ".NET (F#)",
   "language": "F#",
   "name": ".net-fsharp"
  },
  "language_info": {
   "name": "F#"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
